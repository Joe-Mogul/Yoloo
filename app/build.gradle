apply plugin: 'com.android.application'
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'realm-android'
apply plugin: 'com.jakewharton.hugo'
//apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.github.ben-manes.versions'
apply from: "$project.rootDir/tools/quality.gradle"
apply from: "$project.rootDir/tools/script-git-version.gradle"
apply plugin: "com.jaredsburrows.license"

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def secretPropertiesFile = rootProject.file("secret.properties")
def secretProperties = new Properties()
secretProperties.load(new FileInputStream(secretPropertiesFile))

android {
  compileSdkVersion compileSdk
  buildToolsVersion buildTools

  defaultConfig {
    applicationId "com.yoloo.android"
    minSdkVersion minSdk
    targetSdkVersion targetSdk

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    multiDexEnabled true

    vectorDrawables.useSupportLibrary = true
  }

  dexOptions {
    preDexLibraries true
    jumboMode true
  }

  splits {
    abi {
      enable false
      reset()
      include 'x86', 'armeabi', 'armeabi-v7a', 'mips'
      universalApk true
    }
  }

  signingConfigs {
    release {
      keyAlias keystoreProperties['keyAlias']
      keyPassword keystoreProperties['keyPassword']
      storePassword keystoreProperties['storePassword']
      storeFile file(keystoreProperties['storeFile'])
    }

    debug {
      keyAlias 'androiddebugkey'
      keyPassword 'android'
      storePassword 'android'
      storeFile file('../keystore/debug.keystore')
    }
  }

  flavorDimensions "api"

  productFlavors {
    dev {
      dimension "api"
      signingConfig signingConfigs.debug
      versionCode gitVersionCodeTime
      versionName gitVersionName
      // dev utilizes minSDKVersion = 21 to allow the Android gradle plugin
      // to pre-dex each module and produce an APK that can be tested on
      // Android Lollipop without time consuming dex merging processes.
      minSdkVersion 21
    }

    prod {
      dimension "api"
      signingConfig signingConfigs.release
      versionCode gitVersionCode
      versionName gitVersionName
      // The actual minSdkVersion for the application.
      minSdkVersion 16
    }
  }

  buildTypes {
    release {
      debuggable false
      minifyEnabled false
      shrinkResources false
      proguardFiles getDefaultProguardFile('proguard-android.txt'),
          "$project.rootDir/tools/rules-proguard.pro"
      signingConfig signingConfigs.release
      buildConfigField "String", "SERVER_URL", '"https://yoloo-151719.appspot.com/_ah/api/"'
      buildConfigField "String", "UPLOAD_URL", '"https://yoloo-151719.appspot.com/upload"'
      buildConfigField "String", "IN_APP_KEY", secretProperties['inAppBillingKey']
    }

    debug {
      debuggable true
      minifyEnabled false
      shrinkResources false
      versionNameSuffix ".debug"
      signingConfig signingConfigs.debug
      buildConfigField "String", "SERVER_URL", '"https://yoloo-151719.appspot.com/_ah/api/"'
      buildConfigField "String", "UPLOAD_URL", '"https://yoloo-151719.appspot.com/upload"'
      buildConfigField "String", "IN_APP_KEY", secretProperties['inAppBillingKey']
    }
  }

  lintOptions {
    checkReleaseBuilds true
    abortOnError false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

project.android.buildTypes.all { buildType ->
  buildType.javaCompileOptions.annotationProcessorOptions.arguments =
      [validateEpoxyModelUsage     : String.valueOf(buildType.name == 'debug'),
       requireHashCodeInEpoxyModels: "true",
       implicitlyAddAutoModels     : "true"]
}

configurations.all {
  resolutionStrategy {
    eachDependency { DependencyResolveDetails details ->
      final name = details.requested.name
      // force dependencies to use latest version of support libraries
      if (details.requested.group == 'com.android.support' && !name.contains('multidex')) {
        details.useVersion "$supportLibVersion"
      }
    }
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  endpointsServer project(path: ':backend', configuration: 'endpoints')
  implementation project(':nachos')
  implementation('com.google.api-client:google-api-client-android:1.22.0') {
    exclude group: 'org.apache.httpcomponents', module: 'httpclient'
  }

  // Android
  implementation "com.android.support:appcompat-v7:$supportLibVersion"
  implementation "com.android.support:design:$supportLibVersion"
  implementation "com.android.support:recyclerview-v7:$supportLibVersion"
  implementation "com.android.support:cardview-v7:$supportLibVersion"
  implementation "com.android.support:support-compat:$supportLibVersion"
  implementation "com.android.support:customtabs:$supportLibVersion"
  implementation "com.android.support:support-vector-drawable:$supportLibVersion"
  implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"
  implementation "com.android.support:multidex:$multidexVersion"

  // Firebase
  prodImplementation "com.google.firebase:firebase-core:$firebaseVersion"
  implementation "com.google.firebase:firebase-auth:$firebaseVersion"
  implementation "com.google.firebase:firebase-messaging:$firebaseVersion"
  implementation "com.google.firebase:firebase-invites:$firebaseVersion"
  implementation "com.google.firebase:firebase-database:$firebaseVersion"
  implementation "com.google.firebase:firebase-crash:$firebaseVersion"

  // Google Play Services
  implementation "com.google.android.gms:play-services-auth:$playServicesVersion"

  // Conductor
  implementation "com.bluelinelabs:conductor:$conductorVersion"
  implementation "com.bluelinelabs:conductor-support:$conductorVersion"

  // RxJava 2
  implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
  implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

  // Epoxy
  implementation "com.airbnb.android:epoxy:$epoxyVersion"
  annotationProcessor "com.airbnb.android:epoxy-processor:$epoxyVersion"

  // ButterKnife
  implementation "com.jakewharton:butterknife:$butterknifeVersion"
  annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

  // Network
  implementation 'com.squareup.okhttp3:okhttp:3.8.0'
  implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.8.0'

  // Utility
  implementation 'com.evernote:android-job:1.1.10'
  implementation 'org.solovyev.android:checkout:1.0.0'
  implementation "com.annimon:stream:$annimonStreamVersion"
  implementation 'com.github.bumptech.glide:glide:3.8.0'
  implementation "com.jakewharton.timber:timber:$timberVersion"
  implementation 'com.github.adhishlal:asdp:1.1'
  implementation 'uk.co.chrisjenx:calligraphy:2.2.0'
  implementation 'com.squareup.moshi:moshi:1.5.0'
  implementation 'com.github.jksiezni.permissive:permissive:0.2'
  implementation "com.google.android:flexbox:$flexboxVersion"
  implementation 'com.airbnb.android:lottie:2.0.0-rc1'

  implementation "com.jakewharton.rxbinding2:rxbinding:$rxbindingVersion"
  implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:$rxbindingVersion"
  implementation "com.jakewharton.rxbinding2:rxbinding-design:$rxbindingVersion"
  implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rxbindingVersion"

  compile 'com.hannesdorfmann.mosby3:mvi:3.0.4'
  compile 'com.hannesdorfmann.mosby3:mvi-conductor:3.0.0'

  // Views
  implementation('com.sandrios.android:sandriosCamera:1.0.9', {
    exclude group: 'gun0912.ted'
  })
  implementation 'com.github.chrisbanes:PhotoView:2.0.0'
  implementation 'com.yalantis:ucrop:2.2.0'
  implementation 'com.github.claudiodegio:MsvSearch:1.2.0'
  implementation('com.github.igalata:Bubble-Picker:v0.2.4', {
    exclude group: 'io.reactivex'
    exclude group: 'org.slf4j'
  })
  implementation 'com.github.k0shk0sh:RetainedDateTimePickers:1.0.2'
  implementation 'com.github.ekimual:country-picker-x:1.0.0'
  implementation 'com.github.stfalcon:chatkit:0.2.1'
  implementation 'com.github.florent37:tutoshowcase:1.0.1'
  implementation 'com.rafakob:floatingedittext:1.0.5'

  implementation 'com.facebook.android:facebook-android-sdk:4.22.1'

  implementation 'org.parceler:parceler-api:1.1.8'
  annotationProcessor 'org.parceler:parceler:1.1.8'

  annotationProcessor 'dk.ilios:realmfieldnameshelper:1.1.1'

  // Testing
  implementation 'com.facebook.stetho:stetho:1.5.0'
  implementation 'com.uphyca:stetho_realm:2.0.0'

  implementation "junit:junit:$junitVersion"

  androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
    exclude group: 'com.google.code.findbugs', module: 'jsr305'
  })
}

// Put google_services.json
apply plugin: 'com.google.gms.google-services'